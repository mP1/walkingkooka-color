(* common ............................................................................................................*)

PARENTHESIS_CLOSE = ")";
PARENTHESIS_OPEN  = "(";
SEPARATOR         = ",";
WHITESPACE        = " ";

PERCENTAGE        = NUMBER, "%";
ALPHA             = PERCENTAGE | NUMBER;

(* rgb ...............................................................................................................*)
RGB_RGBA             = "rgba" | "rgb";

RGB_ARGUMENTS_PERCENTAGE_COMMA      = PERCENTAGE, [WHITESPACE], SEPARATOR, [WHITESPACE],
                                      PERCENTAGE, [WHITESPACE], SEPARATOR, [WHITESPACE],
                                      PERCENTAGE,
                                      [ [WHITESPACE], SEPARATOR, [WHITESPACE], ALPHA ];

RGB_ARGUMENTS_PERCENTAGE_WHITESPACE = PERCENTAGE, WHITESPACE,
                                      PERCENTAGE, WHITESPACE,
                                      PERCENTAGE,
                                      [ [WHITESPACE], "/", [WHITESPACE], ALPHA ];

RGB_ARGUMENTS_NUMBER_COMMA          = NUMBER, [WHITESPACE], SEPARATOR, [WHITESPACE],
                                      NUMBER, [WHITESPACE], SEPARATOR, [WHITESPACE],
                                      NUMBER,
                                      [ [WHITESPACE], SEPARATOR, [WHITESPACE], ALPHA ];

RGB_ARGUMENTS_NUMBER_WHITESPACE     = NUMBER, WHITESPACE,
                                      NUMBER, WHITESPACE,
                                      NUMBER,
                                      [ [WHITESPACE], "/", [WHITESPACE], ALPHA ];

RGB_RGBA_FUNCTION                   = RGB_RGBA,
                                      PARENTHESIS_OPEN,
                                      [WHITESPACE],
                                      ( RGB_ARGUMENTS_PERCENTAGE_COMMA | RGB_ARGUMENTS_PERCENTAGE_WHITESPACE | RGB_ARGUMENTS_NUMBER_COMMA | RGB_ARGUMENTS_NUMBER_WHITESPACE ),
                                      [WHITESPACE],
                                      PARENTHESIS_CLOSE;

(* hsl/hsv common ....................................................................................................*)
HUE                                     = ( NUMBER, DEGREE_UNIT ) |
                                          NUMBER;

HSL_HSV_ARGUMENTS_PERCENTAGE_COMMA      = HUE, [WHITESPACE], SEPARATOR, [WHITESPACE],
                                          PERCENTAGE, [WHITESPACE], SEPARATOR, [WHITESPACE],
                                          PERCENTAGE,
                                          [ [WHITESPACE], SEPARATOR, [WHITESPACE], ALPHA ];

HSL_HSV_ARGUMENTS_PERCENTAGE_WHITESPACE = HUE, WHITESPACE,
                                          PERCENTAGE, WHITESPACE,
                                          PERCENTAGE,
                                          [ [WHITESPACE], "/", [WHITESPACE], ALPHA ];

HSL_HSV_ARGUMENTS_NUMBER_COMMA          = HUE, [WHITESPACE], SEPARATOR, [WHITESPACE],
                                          NUMBER, [WHITESPACE], SEPARATOR, [WHITESPACE],
                                          NUMBER,
                                          [ [WHITESPACE], SEPARATOR, [WHITESPACE], ALPHA ];

HSL_HSV_ARGUMENTS_NUMBER_WHITESPACE     = HUE, WHITESPACE,
                                          NUMBER, WHITESPACE,
                                          NUMBER,
                                          [ [WHITESPACE], "/", [WHITESPACE], ALPHA ];

(* hsl ...............................................................................................................*)
HSL_HSLA          = "hsla" | "hsl";

HSL_HSLA_FUNCTION = HSL_HSLA,
                    PARENTHESIS_OPEN,
                    [WHITESPACE],
                    ( HSL_HSV_ARGUMENTS_PERCENTAGE_COMMA | HSL_HSV_ARGUMENTS_PERCENTAGE_WHITESPACE | HSL_HSV_ARGUMENTS_NUMBER_COMMA | HSL_HSV_ARGUMENTS_NUMBER_WHITESPACE ),
                    [WHITESPACE],
                    PARENTHESIS_CLOSE;

(* hsv ...............................................................................................................*)
HSV_HSVA          = "hsva" | "hsv";

HSV_HSVA_FUNCTION = HSV_HSVA,
                    PARENTHESIS_OPEN,
                    [WHITESPACE],
                    ( HSL_HSV_ARGUMENTS_PERCENTAGE_COMMA | HSL_HSV_ARGUMENTS_PERCENTAGE_WHITESPACE | HSL_HSV_ARGUMENTS_NUMBER_COMMA | HSL_HSV_ARGUMENTS_NUMBER_WHITESPACE ),
                    [WHITESPACE],
                    PARENTHESIS_CLOSE;